plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'pro.noty'
version = '1.3.0'
description = 'Shows colorized health and Vault economy above entities'

repositories {
    mavenCentral()
    maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url = "https://repo.geysermc.org/repository/maven-public/" }
    maven { url = "https://jitpack.io" }
}

dependencies {
    // Spigot API
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'

    // Vault API
    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        exclude group: "org.bukkit", module: "bukkit"
    }

    compileOnly files('libs/floodgate-api.jar')
    compileOnly files('libs/geyser-api.jar')
}

tasks {
    runServer {
        minecraftVersion("1.21.8")
        pluginJars.from(tasks.jar)
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
